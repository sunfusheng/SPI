apply plugin: 'groovy'
apply plugin: 'maven'

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.android.tools.build:gradle:3.2.1'
    implementation 'org.ow2.asm:asm:6.0'
}

if (isLocalDevelop.toBoolean()) {
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.groupId = 'com.sunfusheng'
                pom.artifactId = 'spi-plugin'
                pom.version = spi_version
                repository(url: uri('../local_maven'))
            }
        }
    }

    task sourcesJar(type: Jar) {
        from project.sourceSets.main.groovy.srcDirs
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: groovydoc) {
        classifier = 'javadoc'
        from groovydoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
} else {
    apply plugin: 'com.novoda.bintray-release'

    publish {
        userOrg = 'sfsheng0322'
        groupId = 'com.sunfusheng'
        artifactId = 'spi-plugin'
        publishVersion = spi_version
        desc = 'SPI Plugin'
        website = 'https://github.com/sunfusheng/SPI'
    }
}